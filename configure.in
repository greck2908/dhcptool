#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.
#

AC_PREREQ(2.59)
AC_INIT(dhcptool, 0.8b, dhcptool@gatorhole.com)
#AM_INIT_AUTOMAKE(dhcptool, 0.8b)
AC_CONFIG_SRCDIR([dhcptool.c])

# Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL

AC_ARG_WITH(includes,
  [AC_HELP_STRING(
    [--with-includes=LIST],
    [add additional include paths. LIST is a colon-separated list of include paths to 
add e.g. --with-includes=/opt/local/include/libnet:/opt/local/include/libpcap])
  ],
  [ ac_addl_includes=`echo "$withval" | sed -e 's/:/ /g'` ;
    for ainclude in $ac_addl_includes; do
      if test x"$ac_build_addl_includes" = x ; then
        ac_build_addl_includes="-I$ainclude"
      else
        ac_build_addl_includes="-I$ainclude $ac_build_addl_includes"
      fi
    done
    CPPFLAGS="$CPPFLAGS $ac_build_addl_includes"
  ])

AC_ARG_WITH(libraries,
  [AC_HELP_STRING(
    [--with-libraries=LIST],
    [add additional library paths. LIST is a colon-separated list of include paths to 
add e.g. --with-libraries=/opt/local/lib/libnet:/opt/local/lib/libpcap])
  ],
  [ ac_addl_libdirs=`echo "$withval" | sed -e 's/:/ /g'` ;
    for alibdir in $ac_addl_libdirs; do
      if test x"$ac_build_addl_libdirs" = x ; then
        ac_build_addl_libdirs="-L$alibdir"
      else
        ac_build_addl_libdirs="-L$alibdir $ac_build_addl_libdirs"
      fi
    done
    LDFLAGS="$LDFLAGS $ac_build_addl_libdirs"
  ])


# Checks for header files.
AC_CHECK_HEADER([pcap.h],,
        [AC_MSG_ERROR([Failed to find pcap.h, use --with-includes=PATH])])

AC_CHECK_HEADER([libnet.h],,
        [AC_MSG_ERROR([Failed to find libnet.h, use --with-includes=PATH])])

# Checks for libraries.
AC_CHECK_LIB([pcap],[pcap_open_live],,
	[AC_MSG_ERROR([Failed to find libpcap, use --with-libraries=PATH])])

AC_CHECK_LIB([net],[libnet_init],,
	[AC_MSG_ERROR([Failed to find libnet, use --with-libraries=PATH])])


# Checks for typedefs, structures, and compiler characteristics.
# Checks for library functions.

AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)
#AC_SUBST(INSTALL)

AC_OUTPUT(Makefile)
